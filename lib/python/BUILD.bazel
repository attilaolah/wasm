load("//lib:defs.bzl", "dep_dir", "lib_package", "static_lib")
load("//toolchains/make:configure.bzl", "configure_make_lib")
load(":package.bzl", "lname")

package(default_visibility = ["//:__subpackages__"])

lib_package(
    version_regex = r">\s*Download Python ([^<]+)\s*</a>",
    version_url = "https://www.python.org/downloads/",
)

# TODO: Missing optional dependencies:
# - Tcl/Tk

CONFIGURE_OPTIONS = [
    # keep sorted
    "--disable-shared",
    '--with-openssl="{}"'.format(dep_dir("openssl")),
    "--with-system-libmpdec",
]

configure_make_lib(
    name = "python",
    configure_options = select({
        ":copt": CONFIGURE_OPTIONS + [
            # TODO: Add llvm-profdata to dependencies for this to work!
            #"--enable-optimizations",
        ],
        "//conditions:default": CONFIGURE_OPTIONS,
    }),
    out_binaries = select({
        "//config:wasm": [],
        "//conditions:default": ["python3"],
    }),
    out_static_libs = [static_lib(lname())],
    deps = [
        "//lib/ffi",
        "//lib/mpdecimal",
        "//lib/openssl",
        "//lib/z",
    ],
)

filegroup(
    name = "runtime",
    srcs = [":python"],
    output_group = "gen_dir",
)

config_setting(
    name = "copt",
    values = {
        "compilation_mode": "opt",
    },
)
