load("//lib:defs.bzl", "include_dir", "library_dir", "static_lib")
load("//lib:lib.bzl", "package_lib")
load("//toolchains/make:configure.bzl", "configure_make_lib")
load(":package.bzl", "NAME", "STATIC_LIBS")

package(default_visibility = ["//:__subpackages__"])

package_lib(
    version_regex = r">\s*openssl-([^<]+)\.tar\.gz\s*<",
    version_url = "https://www.openssl.org/source",
)

CONFIGURE_OPTIONS = [
    # Disable cross-compile prefix.
    # Blaze will use absolute paths for the compiler anyway.
    "--cross-compile-prefix=",
    '--with-zlib-include="{}"'.format(include_dir("z")),
    '--with-zlib-lib="{}"'.format(library_dir("z")),
    "--libdir=lib",  # NOT "lib32" or "lib64"
    "--release",
    "zlib",
] + [
    # keep sorted
    "no-shared",
]

CONFIGURE_OPTIONS_WASM = CONFIGURE_OPTIONS + [
    # keep sorted
    "no-afalgeng",
    "no-asm",
    "no-tests",
    "no-ui-console",
]

configure_make_lib(
    name = NAME,
    configure_command = "config",
    configure_options = select({
        "//config:wasm": CONFIGURE_OPTIONS_WASM,
        "//conditions:default": CONFIGURE_OPTIONS,
    }),
    out_static_libs = STATIC_LIBS,
    deps = ["//lib/z"],
)
