load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@rules_foreign_cc//tools/build_defs:cmake.bzl", "cmake_external")
load("//toolchains/emcmake:cmake.bzl", "emcmake_cache_entries")

package(default_visibility = ["//:__subpackages__"])

CACHE_ENTRIES = {
    "BUILD_SHARED_LIBS:BOOL": "OFF",
    "GIF_INCLUDE_DIR:PATH": "${EXT_BUILD_DEPS}/gif_lib/include",
    "GIF_LIBRARY:FILEPATH": "${EXT_BUILD_DEPS}/gif_lib/lib/libgif.a",
    "JPEG_INCLUDE_DIR:PATH": "${EXT_BUILD_DEPS}/jpeg_turbo_lib/include",
    "JPEG_LIBRARY:FILEPATH": "${EXT_BUILD_DEPS}/jpeg_turbo_lib/lib/libjpeg.a",
    "PNG_LIBRARY:FILEPATH": "${EXT_BUILD_DEPS}/png_lib/lib/libpng.a",
    "PNG_PNG_INCLUDE_DIR:PATH": "${EXT_BUILD_DEPS}/png_lib/include",
    "ZLIB_INCLUDE_DIR:PATH": "${EXT_BUILD_DEPS}/z_lib/include",
    "ZLIB_LIBRARY:FILEPATH": "${EXT_BUILD_DEPS}/z_lib/lib/libz.a",

    # Unsupported: Requires SDL.
    "WEBP_BUILD_VWEBP:BOOL": "OFF",
}

BIN_CACHE_KEYS = [
    "WEBP_BUILD_ANIM_UTILS:BOOL",
    "WEBP_BUILD_CWEBP:BOOL",
    "WEBP_BUILD_DWEBP:BOOL",
    "WEBP_BUILD_EXTRAS:BOOL",
    "WEBP_BUILD_GIF2WEBP:BOOL",
    "WEBP_BUILD_IMG2WEBP:BOOL",
    "WEBP_BUILD_WEBPINFO:BOOL",
    "WEBP_BUILD_WEBPMUX:BOOL",
]

LIB_CACHE_ENTRIES = dict(CACHE_ENTRIES.items() + {key: "OFF" for key in BIN_CACHE_KEYS}.items())

BIN_CACHE_ENTRIES = dict(CACHE_ENTRIES.items() + {key: "ON" for key in BIN_CACHE_KEYS}.items() + {
    "TIFF_INCLUDE_DIR:PATH": "${EXT_BUILD_DEPS}/tiff_lib/include",
    "TIFF_LIBRARY:FILEPATH": "${EXT_BUILD_DEPS}/tiff_lib/lib/libtiff.a",
}.items())

cmake_external(
    name = "webp",
    cache_entries = select({
        "//conditions:default": LIB_CACHE_ENTRIES,
        "//config:emscripten": emcmake_cache_entries(LIB_CACHE_ENTRIES),
    }),
    lib_name = "webp_lib",
    lib_source = "@lib_webp//:all",
    static_libraries = ["libwebp.a"],
    deps = [
        "//lib/gif",
        "//lib/jpeg_turbo",
        "//lib/png",
        "//lib/z",
    ],
)

cmake_external(
    name = "binaries",
    binaries = [
        "cwebp",
        "dwebp",
        "gif2webp",
        "img2webp",
        "webp_quality",
        "webpinfo",
        "webpmux",
    ],
    cache_entries = select({
        "//conditions:default": BIN_CACHE_ENTRIES,
        "//config:emscripten": emcmake_cache_entries(BIN_CACHE_ENTRIES),
    }),
    lib_name = "webp_bin",
    lib_source = "@lib_webp//:all",
    deps = [
        "//lib/gif",
        "//lib/jpeg_turbo",
        "//lib/png",
        "//lib/webp",
        "//lib/z",
    ],
)

bzl_library(
    name = "package",
    srcs = ["package.bzl"],
    deps = ["//:http_archive"],
)
