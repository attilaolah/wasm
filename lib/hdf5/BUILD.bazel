load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("//toolchains/cmake:cmake.bzl", "cmake_lib")

package(default_visibility = ["//:__subpackages__"])

_C_FLAGS = " ".join(["-Wno-{}".format(check) for check in [
    # keep sorted
    "incompatible-pointer-types-discards-qualifiers",
    "misleading-indentation",
    "missing-braces",
    "self-assign",
    "sometimes-uninitialized",
    "unknown-warning-option",
    "unused-function",
    "unused-variable",
]])

_CACHE_ENTRIES = {
    "BUILD_SHARED_LIBS": False,
    "BUILD_STATIC_LIBS": True,
    "BUILD_TESTING": False,
    "CMAKE_C_FLAGS": _C_FLAGS,
    "HDF5_BUILD_EXAMPLES": False,
    "HDF5_BUILD_TOOLS": False,
}

_WASM_CACHE_ENTRIES = dict(_CACHE_ENTRIES.items() + {
    # Emscripten doesn't implement getpwuid yet:
    # https://github.com/emscripten-core/emscripten/issues/13219
    "H5_HAVE_GETPWUID": False,
    # Emscripten doesn't support POSIX signals (except kill):
    # https://emscripten.org/docs/porting/pthreads.html#special-considerations
    "H5_HAVE_SIGNAL": False,
}.items())

cmake_lib(
    name = "hdf5",
    cache_entries = {
        "//config:wasm": _WASM_CACHE_ENTRIES,
        "//conditions:default": _CACHE_ENTRIES,
    },
)

bzl_library(
    name = "package",
    srcs = ["package.bzl"],
    deps = ["//:http_archive"],
)
