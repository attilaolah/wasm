load("//lib:lib.bzl", "package_lib")
load("//toolchains:utils.bzl", "no_error")
load("//toolchains/make:configure.bzl", "configure_make_lib")
load(":package.bzl", "NAME")

package(default_visibility = ["//:__subpackages__"])

package_lib()

BUILD_DEPS_K8 = [
    "//lib/nasm:runtime",
]

CONFIGURE_OPTIONS = [
    "--disable-cli",
    "--enable-pic",
    "--enable-static",
]

CONFIGURE_OPTIONS_K8 = CONFIGURE_OPTIONS + [
    "--host=x86_64-pc-linux-gnu",
]

CONFIGURE_OPTIONS_WASM = CONFIGURE_OPTIONS + [
    "--host=wasm32-unknown-emscripten",
    '--extra-cflags="{}"'.format(
        no_error([
            # keep sorted
            "empty-body",
            "unused-but-set-variable",
        ]),
    ),
    "--disable-asm",
]

DBG_ON = ["--enable-debug"]

OPT_ON = ["--enable-lto"]

configure_make_lib(
    name = NAME,
    build_data = {
        "//config:wasm": [],
        "//conditions:default": BUILD_DEPS_K8,
    },
    configure_options = select({
        "//config:k8_dbg": CONFIGURE_OPTIONS_K8 + DBG_ON,
        "//config:k8_fastbuild": CONFIGURE_OPTIONS_K8,
        "//config:k8_opt": CONFIGURE_OPTIONS_K8 + OPT_ON,
        "//config:wasm_dbg": CONFIGURE_OPTIONS_WASM + DBG_ON,
        "//config:wasm_fastbuild": CONFIGURE_OPTIONS_WASM,
        "//config:wasm_opt": CONFIGURE_OPTIONS_WASM + OPT_ON,
    }),
    env = {
        "//config:wasm": {},
        "//conditions:default": {
            "AS": "$(execpath //lib/nasm:runtime)/bin/nasm",
            "ASFLAGS": "",  # override toolchain flags
        },
    },
    env_override = ["ASFLAGS"],
)
