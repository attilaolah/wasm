load("//lib:defs.bzl", "library_path", "root_path", "static_lib")
load("//lib:lib.bzl", "package_lib")
load("//toolchains/cmake:cmake.bzl", "cmake_lib")
load("//toolchains/make:make.bzl", "make_lib")
load(":package.bzl", "NAME", "NAME_C")

package(default_visibility = ["//:__subpackages__"])

package_lib(
    version_regex = r">\s*blas-([^<]+)\.tgz\s*<",
    version_url = "https://www.netlib.org/blas/",
)

# NOTE: Currently building this package requires a working "gfortran" binary.
# TODO: Build gfortran, and set the F18_FC environment variable to that binary.

FC = root_path("$(execpaths //tools/llvm:flang_runtime)/bin/flang")

cmake_lib(
    name = NAME,
    build_data = ["//tools/llvm:flang_runtime"],
    env = {"FC": FC.replace("${", "$${")},
)

make_lib(
    name = NAME_C,
    args = [
        'BLIB="{}"'.format(library_path(NAME)),
        'CBLIB="../{}"'.format(static_lib(NAME_C)),
        'CFLAGS="-Wno-header-guard"',
        'FC="{}"'.format(FC),
    ],
    build_data = ["//tools/llvm:flang_runtime"],
    postfix_script = 'cp --dereference "{build_tmpdir}/{static_lib}" "{install_dir}/lib"'.format(
        build_tmpdir = "${BUILD_TMPDIR}",
        install_dir = "${INSTALLDIR}",
        static_lib = static_lib(NAME_C),
    ),
    targets = ["alllib"],
    deps = [":" + NAME],
)
