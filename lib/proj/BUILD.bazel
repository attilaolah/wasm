load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("//toolchains:utils.bzl", "no_error", "path")
load("//toolchains/cmake:cmake.bzl", "cmake_lib")

package(default_visibility = ["//:__subpackages__"])

BINARIES = [
    # keep sorted
    "cct",
    "cs2cs",
    "geod",
    "gie",
    "proj",
    "projinfo",
    "projsync",
]

CACHE_ENTRIES = dict({
    "CMAKE_C_FLAGS": no_error([
        # keep sorted
        "incompatible-library-redeclaration",
    ]),
    "CURL_INCLUDE_DIR": "${EXT_BUILD_DEPS}/curl_lib/include",
    "CURL_LIBRARY": "${EXT_BUILD_DEPS}/curl_lib/lib/libcurl.a",
    "OPENSSL_CRYPTO_LIBRARY": "${EXT_BUILD_DEPS}/openssl_lib/lib/libcrypto.a",
    "OPENSSL_INCLUDE_DIR": "${EXT_BUILD_DEPS}/openssl_lib/include",
    "SQLITE3_INCLUDE_DIR": "${EXT_BUILD_DEPS}/sqlite_lib/include",
    "SQLITE3_LIBRARY": "${EXT_BUILD_DEPS}/sqlite_lib/lib/libsqlite3.a",
    "TIFF_INCLUDE_DIR": "${EXT_BUILD_DEPS}/tiff_lib/include",
    "TIFF_LIBRARY": "${EXT_BUILD_DEPS}/tiff_lib/lib/libtiff.a",
}.items() + {
    "BUILD_{}".format(binary.upper()): False
    for binary in BINARIES + [
        "shared_libs",
        "testing",
    ]
}.items())

CACHE_ENTRIES_WASM = dict(CACHE_ENTRIES.items() + {
    # The C++ library uses exceptions, so enable them explicitly.
    "CMAKE_CXX_FLAGS": "-fexceptions",
}.items())

cmake_lib(
    name = "proj",
    build_data = ["//lib/sqlite:runtime"],
    cache_entries = {
        "//config:wasm": CACHE_ENTRIES_WASM,
        "//conditions:default": CACHE_ENTRIES,
    },
    env = {"PATH": path([
        "${EXT_BUILD_ROOT}/$(execpaths //lib/sqlite:runtime)/bin",
    ])},
    deps = [
        "//lib/curl",
        "//lib/openssl",
        "//lib/sqlite",
        "//lib/tiff",
    ],
)

bzl_library(
    name = "package",
    srcs = ["package.bzl"],
    deps = ["//:http_archive"],
)
