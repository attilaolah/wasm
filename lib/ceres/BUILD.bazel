load("//lib:defs.bzl", "lib_package")
load("//toolchains/cmake:cmake.bzl", "cmake_lib")

package(default_visibility = ["//:__subpackages__"])

lib_package()

# NOTE: Version 2.0.0rc1 fails to detect Eigen3 due to some buggy CMake magic.

SS_LIBS = [
    # keep sorted
    "amd",
    "camd",
    "ccolamd",
    "cholmod",
    "colamd",
    "config",
    "metis",
    "spqr",
]

CACHE_ENTRIES = {
    "BUILD_EXAMPLES": False,
    "BUILD_SHARED_LIBS": False,

    # Needed to make #include <SuiteSparse_config.h> work.
    "CMAKE_CXX_STANDARD_INCLUDE_DIRECTORIES": "${EXT_BUILD_DEPS}/config_lib/include",

    # Add -pthread and -lsupc++ to try_compile() commands.
    # These are only needed when compiling with Clang (GCC works fine without these.)
    "CMAKE_REQUIRED_FLAGS": "-pthread",
    "CMAKE_REQUIRED_LIBRARIES": "-lsupc++",

    # Dependencies:
    "CXSPARSE_INCLUDE_DIR": "${EXT_BUILD_DEPS}/cxsparse_lib/include",
    "CXSPARSE_LIBRARY": "${EXT_BUILD_DEPS}/cxsparse_lib/lib/libcxsparse.a",
    "EIGEN_INCLUDE_DIR": "${EXT_BUILD_DEPS}/eigen_lib/include/eigen3",
    "GLOG_INCLUDE_DIR": "${EXT_BUILD_DEPS}/glog_lib/include",
    "GLOG_LIBRARY": "${EXT_BUILD_DEPS}/glog_lib/lib/libglog.a",

    # SuiteSparse hints.
    # We must list each component since we put them in separate directories.
    "SUITESPARSE_INCLUDE_DIR_HINTS": ";".join([
        "${{EXT_BUILD_DEPS}}/{}_lib/include".format(lib)
        for lib in SS_LIBS
    ]),
    "SUITESPARSE_LIBRARY_DIR_HINTS": ";".join([
        "${{EXT_BUILD_DEPS}}/{}_lib/lib".format(lib)
        for lib in SS_LIBS
    ]),
}

CACHE_ENTRIES_WASM = {key: val for key, val in CACHE_ENTRIES.items() if key not in [
    # keep sorted
    "CMAKE_REQUIRED_FLAGS",
    "CMAKE_REQUIRED_LIBRARIES",
]}

cmake_lib(
    name = "ceres",
    cache_entries = {
        "//config:wasm": CACHE_ENTRIES_WASM,
        "//conditions:default": CACHE_ENTRIES,
    },
    deps = [
        "//lib/eigen",
        "//lib/glog",
        "//lib/suitesparse:amd",
        "//lib/suitesparse:camd",
        "//lib/suitesparse:ccolamd",
        "//lib/suitesparse:cholmod",
        "//lib/suitesparse:colamd",
        "//lib/suitesparse:config",
        "//lib/suitesparse:cxsparse",
        "//lib/suitesparse:metis",
        "//lib/suitesparse:spqr",
    ],
)
