load("@bazel_skylib//:bzl_library.bzl", "bzl_library")
load("@rules_foreign_cc//foreign_cc:cmake.bzl", "cmake")

CMAKE_EXE_LINKER_FLAGS = " ".join([
    # Pass in the location of libtinfo.a:
    "-L${EXT_BUILD_DEPS}/ncurses_lib/lib",
    # And force-link against -ltinfo.
    # This is required because we remove the system libtinfo.so from
    # LLVMExports.cmake (see patch_cmds in //tools/llvm:package.bzl for details).
    "-ltinfo",
])

cmake(
    name = "flang",
    # Force some make parallelism, otherwise this build takes over an hour.
    # This will override whatever was passed via --action_env='MAKEFLAGS="-j..."'.
    build_args = ["--parallel=4"],
    build_data = ["@llvm//:all"],
    cache_entries = {
        "BUILD_SHARED_LIBS": "OFF",
        "CMAKE_EXE_LINKER_FLAGS": CMAKE_EXE_LINKER_FLAGS,
        "CMAKE_PREFIX_PATH": "${EXT_BUILD_DEPS}/z_lib",
        "FLANG_INCLUDE_TESTS": "OFF",
        "LIBXML2_INCLUDE_DIR": "${EXT_BUILD_DEPS}/xml_lib/include",
        "LIBXML2_LIBRARY": "${EXT_BUILD_DEPS}/xml_lib/lib/libxml2.a",
        "LLVM_CMAKE_PATH": "${EXT_BUILD_ROOT}/external/llvm/lib/cmake/llvm",
        "MLIR_DIR": "${EXT_BUILD_ROOT}/external/llvm/lib/cmake/mlir",
    },
    lib_source = "@flang//:all",
    out_binaries = [
        "f18",
        "flang",
    ],
    visibility = ["//lib:__subpackages__"],
    deps = [
        "//lib/ncurses",
        "//lib/xml",
        "//lib/z",
    ],
)

filegroup(
    name = "flang_runtime",
    srcs = [":flang"],
    output_group = "gen_dir",
    visibility = ["//lib:__subpackages__"],
)

bzl_library(
    name = "package",
    srcs = ["package.bzl"],
    visibility = ["//tools:__pkg__"],
    deps = [
        "//:http_archive",
        "//:versions",
    ],
)
