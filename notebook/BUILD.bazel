load("@npm//http-server:index.bzl", "http_server")
load("@io_bazel_rules_closure//closure:defs.bzl", "closure_js_binary", "closure_js_library")
load("@npm//@bazel/typescript:index.bzl", "ts_project")
load(":js.bzl", "JS_TARGET", "TS_TARGET", "iife")

package(default_visibility = ["//visibility:public"])

closure_js_binary(
    name = "notebook",
    language = JS_TARGET,
    deps = [":notebook_lib"],
)

closure_js_library(
    name = "notebook_lib",
    srcs = [":notebook_iife"],
    no_closure_library = True,
    suppress = [
        # Closure Compiler currently does not consume TypeScript .d.ts inputs.
        # Therefore it has no clue about the types, so we disable some optimisations.
        "JSC_UNKNOWN_EXPR_TYPE",
    ],
)

iife(
    name = "notebook_iife",
    srcs = [":notebook_ts"],
    replace = {
        "let importJS;": "",
        "importJS": "import",
    },
)

ts_project(
    name = "notebook_ts",
    srcs = ["main.ts"],
    tsconfig = {"compilerOptions": {
        "target": TS_TARGET,
        "esModuleInterop": True,
        "module": "commonjs",
    }},
    deps = ["@npm//@types/emscripten"],
)

filegroup(
    name = "notebook_html",
    srcs = [
        "index.html",
        "notebook.html",
    ],
)

http_server(
    name = "server",
    args = ["notebook/server_files"],
    data = [":server_files"],
)

genrule(
    name = "server_files",
    srcs = [
        ":notebook_html",
        "//notebook/runtime",
        "//notebook/style:themes",
    ] + select({
        "//config:opt": [
            ":notebook",
        ],
        "//conditions:default": [
            ":notebook_iife",
        ],
    }),
    outs = ["server_files"],
    cmd = r"""
mkdir -p "$@/modules"
cp $(locations :notebook_html) "$@"
cp "$(RULEDIR)/runtime/runtime.wasm" "$@/modules"

mkdir -p "$@/themes"
cp $(locations //notebook/style:themes) "$@/themes"
""" + select({
        "//config:opt": """
cp "$(RULEDIR)/notebook.js" "$@/notebook.mjs"
cp "$(RULEDIR)/runtime/runtime_wasm_js_3.js" "$@/modules/runtime.mjs"
""",
        "//conditions:default": """
cp "$(location :notebook_iife)" "$@/notebook.mjs"
cp "$(RULEDIR)/runtime/runtime.js" "$@/modules/runtime.mjs"
""",
    }),
    local = True,
)
